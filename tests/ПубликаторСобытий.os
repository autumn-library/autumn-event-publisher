// BSLLS:MissingVariablesDescription-off

#Использовать ".."
#Использовать "."
#Использовать asserts
#Использовать autumn

Перем ПереданныйИсточник;
Перем ПереданныйПараметр;

&ПередЗапускомТеста
Процедура ПередЗапускомТеста() Экспорт
	ПереданныйПараметр = 0;
	ПереданныйИсточник = Неопределено;
КонецПроцедуры

Процедура ПослеЗапускаТеста() Экспорт
КонецПроцедуры

Процедура ОбработчикТестовогоСобытия(Источник, Параметр) Экспорт

	ПереданныйИсточник = Источник;
	ПереданныйПараметр = Параметр;
	
КонецПроцедуры

&Тест
Процедура ПубликацияСобытияОтправляетсяДвижку() Экспорт

	// Дано
	Поделка = Новый Поделка;
	Поделка.ЗапуститьПриложение();

	ПубликаторСобытий = Поделка.НайтиЖелудь("ПубликаторСобытий");

	ДобавитьОбработчик ПубликаторСобытий.ТестовоеСобытие, ЭтотОбъект.ОбработчикТестовогоСобытия;

	ЗначениеПараметра = 1;
	ПараметрыСобытия = Новый Массив;
	ПараметрыСобытия.Добавить(ЗначениеПараметра);
	
	// Когда
	ПубликаторСобытий.ОпубликоватьСобытие(ЭтотОбъект, "ТестовоеСобытие", ПараметрыСобытия);
	
	// Тогда
	Ожидаем.Что(ПереданныйИсточник, "Источник события передался корректно").Равно(ЭтотОбъект);
	Ожидаем.Что(ПереданныйПараметр, "Параметр события передался корректно").Равно(ЗначениеПараметра);

КонецПроцедуры

&Тест
Процедура ПубликацияСобытияОтправляетсяЖелудям() Экспорт

	// Дано
	Поделка = Новый Поделка;
	Поделка.ЗапуститьПриложение();

	// TODO: Проверка, что подписка вызывается до явного создания желудя
	ПодписчикНаТестовоеСобытие = Поделка.НайтиЖелудь("ПодписчикНаТестовоеСобытие");
	ПубликаторСобытий = Поделка.НайтиЖелудь("ПубликаторСобытий");

	ЗначениеПараметра = 1;
	ПараметрыСобытия = Новый Массив;
	ПараметрыСобытия.Добавить(ЗначениеПараметра);
	
	// Когда
	ПубликаторСобытий.ОпубликоватьСобытие(ЭтотОбъект, "ТестовоеСобытие", ПараметрыСобытия);
	ПубликаторСобытий.ОпубликоватьСобытие(ЭтотОбъект, "ТестовоеСобытие", ПараметрыСобытия);
	
	// Тогда
	Ожидаем.Что(ПодписчикНаТестовоеСобытие.ПереданныйПараметр, "Параметр события передался в желудь корректно")
		.Равно(ЗначениеПараметра);
	Ожидаем.Что(ПодписчикНаТестовоеСобытие.КоличествоСрабатываний, "Подписчик вызвался ровно два раза")
		.Равно(2);

КонецПроцедуры

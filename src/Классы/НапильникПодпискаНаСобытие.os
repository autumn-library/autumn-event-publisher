#Использовать annotations

Перем Лог;

Перем _ПубликаторСобытий;

Функция ОбработатьЖелудь(Желудь, ОпределениеЖелудя) Экспорт
	
	ПодписчикиНаСобытия = ОпределениеЖелудя.НайтиМетодыСАннотациями("ПодпискаНаСобытие");
	
	Для Каждого ПодписчикНаСобытия Из ПодписчикиНаСобытия Цикл

		Аннотация = РаботаСАннотациями.НайтиАннотацию(ПодписчикНаСобытия.Аннотации, "ПодпискаНаСобытие");
		ИмяСобытия = РаботаСАннотациями.ПолучитьЗначениеПараметраАннотации(Аннотация);

		Лог.Отладка(
			"Добавляю подписку на событие ""%1"". Обработчик события: %2.%3",
			ИмяСобытия,
			ОпределениеЖелудя.Имя(),
			ПодписчикНаСобытия.Имя
		);

		ДобавитьОбработчик _ПубликаторСобытий[ИмяСобытия], Желудь[ПодписчикНаСобытия.Имя];

	КонецЦикла;

	Возврат Желудь;

КонецФункции

&Напильник
Процедура ПриСозданииОбъекта(&Пластилин ПубликаторСобытий)
	_ПубликаторСобытий = ПубликаторСобытий;
	Лог = Логирование.ПолучитьЛог("oscript.lib.autumn.event-publisher.НапильникПодпискаНаСобытие");
КонецПроцедуры